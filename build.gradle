plugins {
    id 'com.gradle.plugin-publish' version '0.10.1'
    id "luisrjaeger.auto-versioning" version "1.0.6"
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'groovy'
}

description 'Gradle plugin to publish apks to Airwatch Console API'
ext.pluginId = 'luisrjaeger.airwatch-publishing'
group 'br.com.luisrjaeger'

autoVersion {
    versionCycle = 100
    releaseNotesFileName = null
}

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation "com.squareup.okhttp3:okhttp:3.11.0"
    implementation "com.squareup.okhttp3:logging-interceptor:3.11.0"
}

gradlePlugin {
    plugins {
        airwatchPublishingPlugin {
            id = project.pluginId
            implementationClass = project.group + '.airwatch.AirwatchPublishingPlugin'
        }
    }
}

//publishPlugins { dependsOn increaseVersion }

pluginBundle {
    version = autoVersion.versionName()
    website = 'https://github.com/luisrjaeger'
    vcsUrl = 'https://github.com/luisrjaeger/airwatch-publishing'
    description = project.description
    tags = ['airwatch', 'publishing', 'deploy']
    plugins {
        airwatchPublishingPlugin {
            displayName = 'Airwatch Publishing'
        }
    }
}

publishing {
    publications {
        pluginPublication (MavenPublication) {
            from        components.java
            groupId     project.group
            artifactId  project.name
            version     project.version
        }
    }
    repositories {
        maven {
            url mavenRepoUrl
        }
    }
}